■■SPARQLクエリのサンプル


■必要なリンク集
〇WikidataのSPARQLエンドポイント（検索用API）
https://query.wikidata.org/

〇PREFIXの検索サービス
https://prefix.cc/

〇Wikidataのクラス一覧
https://docs.google.com/spreadsheets/d/1e2r9w-PnAroO4MTAMJGfw-bZV7r68Xul-UtK6QbrK_w/edit#gid=0

〇Wikidataのプロパティ一覧（抜粋）
https://www.wikidata.org/wiki/Wikidata:List_of_properties


■検索例①：主語のみ指定

「大阪大学」を主語（Subject）に含むトリプルの述語（?p）と目的語（?o）を取得する　

-----クエリ-----
select *
where { 
   <http://www.wikidata.org/entity/Q651233> ?p ?o . 
}
LIMIT 100
---------------

検索結果　http://tinyurl.com/ycr2f322


■検索例①’：主語のみ指定

「大阪大学」を主語（Subject）とするトリプルの述語（?p）を取得する（重複除く）

-----クエリ-----
select distinct ?p
where { 
   <http://www.wikidata.org/entity/Q651233> ?p ?o . 

}
LIMIT 100
---------------

　↓　PREFIXを用いた省略表現

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>

select distinct ?p
where { 
   wd:Q651233 ?p ?o . 
}LIMIT 100
---------------


■検索例②：主語と述語を指定

例②-1）「大阪大学」の「本部所在地」となる目的語（?o）を取得

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

select distinct ?o
where { 
   wd:Q651233 wdt:P159 ?o . 
}LIMIT 100
---------------


例②-２） 「大阪大学」のラベルとなる目的語（?o）を取得

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?o
where { 
   wd:Q651233 rdfs:label ?o . 
}LIMIT 100
---------------

　　↓　「言語の種別」を合わせて取得

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?o (lang(?o) AS ?ln)
where { 
   wd:Q651233 rdfs:label ?o . 
}LIMIT 100
---------------



■検索例③：Filterにより絞り込み

「大阪大学」のラベルとなる目的語（?o）から，“日本語のラベルのみ”を取得

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?o
where { 
   wd:Q651233 rdfs:label ?o . 
   FILTER (lang(?o) = "ja") . 
}LIMIT 100
---------------


検索例③'：その他のFilter

「大阪大学」のラベルとなる目的語（?o）のうち，“Osaka”という文字列を含むもの取得

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?o
where { 
   wd:Q651233 rdfs:label ?o . 
   FILTER (regex(?o,"Osaka")) . 
}LIMIT 100
---------------



■検索例④：述語と目的語を指定

例）④-1：「国が“日本”と一致する」トリプルの主語（?s）を取得する

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

select ?s
where { 
 ?s wdt:P17 wd:Q17 . 
}
LIMIT 100
---------------


例）④-2： 「ラベルが“大阪大学”と一致する」トリプルの主語（?s）を取得する

-----クエリ-----
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?s
where { 
 ?s rdfs:label "大阪大学"@ja . 
}
---------------



■検索例⑤：述語と目的語を指定し，主語の一覧を取得

例）⑤-1)「大阪大学」のクラス（何のインスタンスか？）を取得する

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
select ?s
where { 
 ?s wdt:P31 wd:Q3918. 
}LIMIT 100
---------------


例⑤-2)「大学」のインスタンスの一覧を取得する

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
select ?s
where { 
　?s wdt:P31 wd:Q3918. 
}LIMIT 100
---------------


例⑤-3) 「大学」のインスタンスの一覧と，その“日本語ラベル付き”を取得する

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?s ?o where { 
  ?s wdt:P31 wd:Q3918.
  ?s rdfs:label ?o . 
  FILTER (lang(?o) = "ja") .  
}LIMIT 100
---------------


例⑤-4) 「大学」のインスタンスの一覧を取得し，その“日本語ラベル付き”があれば，取得する

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?s ?o where { 
  ?s wdt:P31 wd:Q3918.
  OPTIONAL{
  ?s rdfs:label ?o . 
  FILTER (lang(?o) = "ja") .
 }
}LIMIT 100
---------------

例⑤-5) 「大学」と「卒業生」の組み合わせを取得する

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?s ?univ where { 
 ?univ wdt:P31  wd:Q3918. 
 ?s       wdt:P69  ?univ .
}LIMIT 100
---------------

例⑤-6) 「大学」と「卒業生」の組み合わせを取得する．日本語のラベルを併記

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?univ ?univl ?s ?l where { 
 ?univ wdt:P31 wd:Q3918. 
 ?s wdt:P69 ?univ.
 OPTIONAL{
  ?univ rdfs:label ?univl . 
  FILTER (lang(?univl) = "ja") . 
  ?s rdfs:label ?l . 
  FILTER (lang(?l) = "ja") . 
  }
}LIMIT 100
---------------



■検索例⑥：カウントを利用したランキング

例⑥-1) 「大学」のインスタンスの数を取得する

---------------

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>

select (count (?s) AS ?c) where { 
  ?s wdt:P31 wd:Q3918. 
}
---------------

例⑥-2) 「大学」と「卒業生の数」の組み合わせをランキング

-----クエリ-----
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?univ ?univl (count(?s) As ?c) where { 
  ?univ wdt:P31 wd:Q3918.
  ?s wdt:P69 ?univ.
  OPTIONAL{
    ?univ rdfs:label ?univl . 
    FILTER (lang(?univl) = "ja") . 
   }
  }GROUP BY ?univ ?univl
ORDER BY DESC(?c)
LIMIT 100

